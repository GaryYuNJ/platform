buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
        springDependencyManagementPluginVersion = '1.0.0.RELEASE'
    }

    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:${springDependencyManagementPluginVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    ext {
        springVersion = '4.3.6.RELEASE'
        springBootVersion = '1.5.1.RELEASE'
        springDataVersion = 'Ingalls-RELEASE'
        springSecurityVersion = '4.2.1.RELEASE'
        springSessionVersion = '1.3.0.RELEASE'

        hibernateValidatorVersion = '5.4.0.Final'

        mybatisVersion = '3.4.2'
        mybatisSpringVersion = '1.3.0'
        mybatisSpringBootVersion = '1.2.0'
        mybatisGeneratorVersion = '1.3.5'
        mybatisPlusVersion = '2.0.2'

        mysqlVersion = '5.1.40'
        jedisVersion = '2.9.0'
        slf4jVersion = '1.7.23'
        log4j2Version = '2.8'
        jerseyVersion = '2.25.1'
        assertjVersion = '3.6.1'
        sitemeshVersion = '3.0.1'
        guavaVersion = '21.0'
        shiroVersion = '1.4.0-RC2'
        druidVersion = '1.0.28'
        okHttpVersion = '3.6.0'
        jacksonVersion = '2.8.7'
        asmVersion = '5.2'
        aspectjVersion = '1.8.10'
        ehcacheVersion = '2.10.3'
        jodatimeVersion = '2.9.7'
        javassistVersion = '3.21.0-GA'

        commonsIoVersion = '2.5'
        commonsLangVersion = '3.5'
        commonsCodecVersion = '1.10'
        commonsFileuploadVersion = '1.3.2'
        commonsBeanutilsVersion = '1.9.3'
        commonsCollectionsVersion = '3.2.2'
        commonsPoolVersion = '2.4.2'

        javaxJstlApiVersion = '1.2'
        javaxServletApiVersion = '3.1.0'
        javaxJspApiVersion = '2.3.0'

        tomcatVersion = '8.5.11'
    }

    // 覆盖spring-boot-dependencies的版本定义,保持最新版本
    ext['spring.version'] = "${springVersion}"
    ext['spring-session.version'] = "${springSessionVersion}"
    ext['spring-security.version'] = "${springSecurityVersion}"
    ext['spring-data-releasetrain.version'] = "${springDataVersion}"
    ext['hibernate-validator.version'] = "${hibernateValidatorVersion}"
    ext['aspectj.version'] = "${aspectjVersion}"
    ext['mysql.version'] = "${mysqlVersion}"
    ext['slf4j.version'] = "${slf4jVersion}"
    ext['log4j2.version'] = "${log4j2Version}"
    ext['jackson.version'] = "${jacksonVersion}"
    ext['jedis.version'] = "${jedisVersion}"
    ext['jersey.version'] = "${jerseyVersion}"
    ext['ehcache.version'] = "${ehcacheVersion}"
    ext['joda-time.version'] = "${jodatimeVersion}"
    ext['javassist.version'] = "${javassistVersion}"
    // server
    ext['tomcat.version'] = "${tomcatVersion}"
    // 覆盖spring-boot-dependencies的版本定义,保持最新版本

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    apply plugin: "idea"
    apply plugin: "java"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        compile.exclude group: 'org.apache.geronimo.specs'
        compile.exclude module: 'commons-logging'
        compile.exclude module: 'xml-apis'
        compile.exclude module: 'h2'
        compile.exclude module: 'spring-boot-starter-logging'
        compile.exclude module: 'ehcache-core'
        compile.exclude module: 'persistence-api'
    }

    defaultTasks = ['clean', 'build']
}

project("webapp") {
    description = "Platform"
    version = "0.0.1"

    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'application'

    mainClassName = "cn.elvea.Initializer"

    springBoot {
        applyExcludeRules = false
    }

    configurations {
        providedRuntime
    }

    dependencies {
        // Spring & Spring Boot & Spring Session & Spring Security
        compile("org.springframework.boot:spring-boot-starter-jdbc")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-log4j2")
        compile("org.springframework.boot:spring-boot-starter-data-redis")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-cache")
        compile("org.springframework.boot:spring-boot-devtools")
        compile("org.springframework.session:spring-session")

        // Apache Commons & Shiro
        // Shrio
        compile("org.apache.shiro:shiro-spring:${shiroVersion}")
        compile("org.apache.shiro:shiro-web:${shiroVersion}")
        // Apache Commons
        compile("org.apache.commons:commons-lang3:${commonsLangVersion}")
        compile("org.apache.commons:commons-pool2:${commonsPoolVersion}")
        compile("commons-fileupload:commons-fileupload:${commonsFileuploadVersion}")
        compile("commons-io:commons-io:${commonsIoVersion}")
        compile("commons-beanutils:commons-beanutils:${commonsBeanutilsVersion}")
        compile("commons-codec:commons-codec:${commonsCodecVersion}")

        // ====== Database & DataSource ======
        compile("mysql:mysql-connector-java")
        compile("redis.clients:jedis")
        compile("com.alibaba:druid:${druidVersion}")
        // MyBatis
        compile("org.mybatis:mybatis:${mybatisVersion}")
        compile("org.mybatis:mybatis-spring:${mybatisSpringVersion}")
        compile("org.mybatis.generator:mybatis-generator-core:${mybatisGeneratorVersion}")
        compile("com.baomidou:mybatis-plus:${mybatisPlusVersion}")

        // ====== 3rd party ======
        compile("com.fasterxml.jackson.core:jackson-core:${jacksonVersion}")
        compile("com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}")
        compile("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
        compile("org.aspectj:aspectjrt:${aspectjVersion}")
        compile("org.aspectj:aspectjweaver:${aspectjVersion}")
        compile("org.ow2.asm:asm:${asmVersion}")
        compile("net.sf.ehcache:ehcache")
        compile("joda-time:joda-time")
        compile("com.squareup.okhttp3:okhttp:${okHttpVersion}")
        compile("org.sitemesh:sitemesh:${sitemeshVersion}")
        compile("com.google.guava:guava:${guavaVersion}")

        // javax
        compile("javax.servlet:jstl:${javaxJstlApiVersion}")
        providedCompile("javax.servlet:javax.servlet-api:${javaxServletApiVersion}")
        providedCompile("javax.servlet.jsp:javax.servlet.jsp-api:${javaxJspApiVersion}")

        // tomcat
        providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
        providedRuntime("org.apache.tomcat.embed:tomcat-embed-jasper")

        // test
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}
